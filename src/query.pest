input = _{ SOI ~ _expression ~ EOI }

_expression  = _{ _e_or }
or           =  { _e_xor ~ (_or ~ _e_xor)+ }
xor          =  { _e_and ~ (_xor ~ _e_and)+ }
and          =  { _e_unary ~ (_and ~ _e_unary)+ }
not          =  { _not ~ _e_unary }
_e_or        = _{ or | _e_xor }
_e_xor       = _{ xor | _e_and }
_e_and       = _{ and | _e_unary }
_e_unary     = _{ not | primary }
primary      =  { "(" ~ _expression ~ ")" | term }
term         =  { level_filter | field_filter }
level_filter = @{ ^"level" ~ is ~ level }
field_filter =  { "." ~ field_name ~ string_op ~ string }
field_name   = @{ ("@" | "_" | "-" | "." | LETTER | NUMBER)+ }

level         = ${
    !ASCII_ALPHANUMERIC ~ level_debug
  | level_info
  | level_warning
  | level_error ~ !ASCII_ALPHANUMERIC
}
level_debug   = @{ ^"debug" | ^"dbg" }
level_info    = @{ ^"info" | ^"inf" }
level_warning = @{ ^"warning" | ^"warn" | ^"wrn" }
level_error   = @{ ^"error" | ^"err" }

_or       = _{ ^"or" }
_xor      = _{ ^"xor" }
_and      = _{ ^"and" }
_not      = _{ ^"not" }
is        = @{
    ^"is" ~ &punctuation
}
string_op = _{ equal | like | match | not_equal | not_like | not_match }
equal     = @{
    "="
  | ^"eq" ~ &punctuation
}
like      = @{
    "~="
  | ^"like" ~ &punctuation
}
match     = @{
    "~~="
  | ^"match" ~ &punctuation
}
not_equal = @{
    "!="
  | ^"not" ~ WHITESPACE+ ~ ^"eq" ~ &punctuation
}
not_like  = @{
    "!~="
  | ^"not" ~ WHITESPACE+ ~ ^"like" ~ &punctuation
}
not_match = @{
    "!~~="
  | ^"not" ~ WHITESPACE+ ~ ^"match" ~ &punctuation
}

punctuation = @{ "(" | ")" | WHITESPACE | EOI }

string = ${ json_string | simple_string }

json_string       = @{ "\"" ~ json_string_inner ~ "\"" }
json_string_inner = @{ json_char* }
json_char         =  {
    !("\"" | "\\") ~ ANY
  | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
  | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

simple_string = @{ simple_char* }
simple_char   = @{ (LETTER | NUMBER | "@" | "." | "_" | "-" | ":" | "/" | "!" | "#" | "%" | "$" | "*" | "+")+ }

WHITESPACE = _{ " " }
